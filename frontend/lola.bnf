//////////////////////////////////////////////////
//////////////////// Symbols /////////////////////
//////////////////////////////////////////////////

Symbol ::= unicode
Letter ::= 'a'..'z' | 'A'..'Z'
Digit ::= 0..9


//////////////////////////////////////////////////
//////////// Operators and Functions /////////////
//////////////////////////////////////////////////

CompOp ::= '<' | '<=' | '>' | '>=' | '=' | '≤' | '≥' | '!=' | '≠'

BinaryFunctionSymbol ::=
      'nroot'
    | 'π' // Tuple Projection

UnaryFunctionSymbol ::=
      'sin'
    | 'cos'
    | 'tan'
    | 'arcsin'
    | 'arccos'
    | 'arctan'
    | 'exp'
    | 'floor'
    | 'ceil'
    | 'sqrt'

WindowOp ::= 'Σ' | 'Π' | 'avg' | 'count' | '∫' | 'sum' | 'prod' | '#' | 'integral'

FunctionSymbol ::= UnaryFunctionSymbol | BinaryFunctionSymbol | Ident


//////////////////////////////////////////////////
//////////////////// Literals ////////////////////
//////////////////////////////////////////////////

NumberLiteral ::= ('+' | '-')? Digit* ('.' Digit*)? ('e' ('+' | '-')? Digit*)?

StringLiteral ::= '"' (Symbol - '"' | Digit)* '"'

TupleLiteral ::= '(' (Literal (',' Literal)*)? ')'

BooleanLiteral ::= 'true' | 'false' | '⊤' | '⊥'

Literal ::= StringLiteral | NumberLiteral | TupleLiteral | BooleanLiteral

IntegerLiteral ::= Digit*

PType ::= 'Int8' | 'Int16' | 'Int32' | 'Int64' | 'Bool' | 'Double' | 'String'

Type ::= '(' (Type (',' Type)*)? ')' | Ident | PType


//////////////////////////////////////////////////
////////////////// Time Related //////////////////
//////////////////////////////////////////////////

Duration ::= NumberLiteral UnitOfTime

UnitOfTime ::= | 'ns' | 'μs' | 'ms' | 's' | 'min' | 'h' | 'd' | 'w'

Frequency ::= Duration | NumberLiteral UnitOfFreq

UnitOfFreq ::= 'μHz' | 'mHz' | 'Hz' | 'kHz' | 'MHz'


//////////////////////////////////////////////////
///////////////////// Names //////////////////////
//////////////////////////////////////////////////

Ident ::=  (Letter | '_') (Letter | Digit | '_')*

Parameter ::= Ident // Semantic Category

EffectiveConstant ::= Parameter | Literal

StreamInstance ::= Ident ('(' EffectiveConstant (',' EffectiveConstant)* ')')?


//////////////////////////////////////////////////
////////////////// Expressions ///////////////////
//////////////////////////////////////////////////

// Precedences:
// Atomic < TernaryExpr < BooleanDisExpr < BooleanConExpr 
//      < CompExpr < AddExpr < MultiExpr < ExpoExpr < UnaryExpr < DefaultExpr 
//      < FunctionExpr < LookupExpr < CompoundExpr

Expr ::= CompoundExpr

CompoundExpr ::=
      LookupExpr
    | Ident '.' Ident

LookupExpr ::=
      FunctionExpr
    | StreamInstance '[' EffectiveConstant ']' 
    | StreamInstance '[' Duration ',' WindowOp ']'
    | StreamInstance '[' Duration ']'

FunctionExpr ::= 
      DefaultExpr
    | FunctionSymbol '(' Expr (',' Expr)* ')' 
    | 'π' '(' IntegerLiteral ',' Ident ')'

DefaultExpr ::= 
      UnaryExpr 
    | LookupExpr '?' Expr

UnaryExpr ::=
      ExpoExpr
    | '+' UnaryExpr
    | '-' UnaryExpr
    | '!' UnaryExpr

ExpoExpr ::=
      DefaultExpr
    | MultiExpr '**' ExpoExpr

MultiExpr ::=
      AddExpr 
    | MultiExpr '*' AddExpr 
    | MultiExpr '/' AddExpr 
    | MultiExpr '%' AddExpr

AddExpr ::=
      CompExpr
    | AddExpr '+' CompExpr
    | AddExpr '-' CompExpr

CompExpr ::=
      BooleanConExpr
    | BooleanConExpr CompOp BooleanConExpr

BooleanConExpr ::=
      BooleanDisExpr
    | BooleanConExpr '∧' BooleanDisExpr
    | BooleanConExpr '&' BooleanDisExpr
    | BooleanConExpr 'and' BooleanDisExpr

BooleanDisExpr ::=
      TernaryExpr
    | BooleanDisExpr '∨' TernaryExpr
    | BooleanDisExpr '|' TernaryExpr
    | BooleanDisExpr 'or' TernaryExpr

TernaryExpr ::=
      AtomicExpr
    | 'if' Expr 'then' Expr 'else' Expr

AtomicExpr ::= 
      EffectiveConstant 
    | Ident
    | '(' Expr ')'
    | '(' (Expr (',' Expr)+)? ')'


//////////////////////////////////////////////////
/////////////////// Statements ///////////////////
//////////////////////////////////////////////////

ParamList ::= '<' Type Ident (',' Type Ident)* '>'

TemplateSpec ::= 
      '{' 
        ('invoke' Expr ('if' | 'unless') Expr)? 
        ('extend' Expr? ('@' Frequency)?)?
        ('terminate' Expr)?
      '}'

IncludeStatement ::= 'include' StringLiteral

TypeDecl ::= 'Type' Ident '{' Type Ident (',' Type Ident)+ '}'

ConstantStream ::= 'constant' PType Ident ':=' Literal

InputStream ::= 'input' PType Ident ParamList? (',' Ident ParamList?)*

OutputStream ::= 'output' Type Ident ParamList? TemplateSpec? ':=' Expr

Trigger ::= 'trigger' Ident? Expr StringLiteral?

LanguageSpec ::= '//!' ('ClassicLola' | 'Lola2.0' | 'RTLola')

Spec ::= LanguageSpec? (IncludeStatement | TypeDecl | ConstantStream |
InputStream | OutputStream | Trigger)*